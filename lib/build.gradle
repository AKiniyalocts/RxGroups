apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'me.tatarka.retrolambda'

checkstyle {
  configFile rootProject.file('checkstyle.xml')
  showViolations true
  configProperties = ['checkstyle.cache.file': rootProject.file('build/checkstyle.cache')]
}

sourceCompatibility = 1.8

project.group = 'com.airbnb.rxgroups'
project.version = PROJECT_VERSION

dependencies {
  compile 'io.reactivex:rxjava:1.1.0'
  compile 'com.google.code.findbugs:jsr305:3.0.0'

  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:1.7.0'
}

if (JavaVersion.current().isJava8Compatible()) {
  allprojects {
    tasks.withType(Javadoc) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }
}

retrolambda {
  oldJdk System.getenv("JAVA7_HOME")
}

modifyPom {
  project {
    name 'RxGroups library'
    description PROJECT_DESCRIPTION
    url PROJECT_URL
    inceptionYear '2015'
    version PROJECT_VERSION
    groupId PROJECT_GROUP_ID
    artifactId 'rxgroups'

    scm {
      url PROJECT_URL
      connection 'scm:git@git.musta.ch:airbnb/rxgroups.git'
      developerConnection 'scm:git@git.musta.ch:airbnb/rxgroups.git'
    }

    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }

    developers {
      developer {
        id 'airbnb'
        name 'Airbnb'
        email 'android@airbnb.com'
      }
    }
  }
}

extraArchive {
  sources = true
  tests = false
  javadoc = true
}

nexus {
  sign = !isCi()
  repositoryUrl = 'http://airbnb-maven-master.d.musta.ch/nexus/content/repositories/releases/'
  snapshotRepositoryUrl = 'http://airbnb-maven-master.d.musta.ch/nexus/content/repositories/snapshots/'
}
